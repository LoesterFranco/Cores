; ============================================================================
;        __
;   \\__/ o\    (C) 2020  Robert Finch, Stratford
;    \  __ /    All rights reserved.
;     \/_//     robfinch<remove>@finitron.ca
;       ||
;  
;
; This source file is free software: you can redistribute it and/or modify 
; it under the terms of the GNU Lesser General Public License as published 
; by the Free Software Foundation, either version 3 of the License, or     
; (at your option) any later version.                                      
;                                                                          
; This source file is distributed in the hope that it will be useful,      
; but WITHOUT ANY WARRANTY; without even the implied warranty of           
; MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the            
; GNU General Public License for more details.                             
;                                                                          
; You should have received a copy of the GNU General Public License        
; along with this program.  If not, see <http://www.gnu.org/licenses/>.    
;
; ============================================================================

NPAGES	equ		$4300
PAM			equ		$4800

		code	18 bits
		align	4
;------------------------------------------------------------------------------
;------------------------------------------------------------------------------

MMUInit:
		sub		$sp,$sp,#4
		stt		$ra,[$sp]
		ldi		$t0,#447				; set number of available pages (65 pages already allocated)
		stt		$t0,NPAGES
		; Free all memory for all tasks			
		ldi		$t0,#$00
		ldi		$t4,#15					; task id
.0004:
		ldi		$t1,#$000				; regno
		ldi		$t2,#512				; number of registers to update
		ldi		$t3,#64					; number of pages pre-allocated
.0001:
		sll		$s1,$t4,#16			; put tid in place
		or		$s1,$s1,$t1			; or in page number
		mvmap	$x0,$t0,$s1
		add		$t0,$t0,#$01
		add		$t1,$t1,#$01		; increment page number
		bltu	$t1,$t3,.0003
		mov		$t0,$x0					; mark pages unallocated
.0003:
		sub		$t2,$t2,#1
		bne		$t2,$x0,.0001
		sub		$t4,$t4,#1
		bge		$t4,$x0,.0004
		; Now ensure all pages in PAM are marked free
		ldi		$t0,#511
.0006:
		pfree	$t0
		sub		$t0,$t0,#1
		bge		$t0,$x0,.0006
		; allocate last page for system stack
		ldi		$t1,#$1FF				
		ldi		$t0,#$1FF
		mvmap	$x0,$t0,$t1

		; Now setup segment registers
		ldi		$t0,#$0
		ldi		$t1,#$07				; t1 = value to load RWX=111, base = 0
.0002:
		mvseg	$x0,$t1,$t0			; move to the segment register identifed by t0
		add		$t0,$t0,#1			; pick next segment register
		slt		$t2,$t0,#16			; 16 segment regs
		bne		$t2,$x0,.0002
		ldt		$ra,[$sp]
		add		$sp,$sp,#4
		ret

;------------------------------------------------------------------------------
; Find a run of buckets available for mapping virtual to physical addresses.
;
; Parameters:
;		a0 = pid
;		a1 = number of pages required.
; Modifies:
;		t1,t2,t3,t5
; Returns:
;		v0 = starting bucket number (includes ASID), -1 if no run found
;------------------------------------------------------------------------------

FindRun:
	and			$t3,$a0,#$0F			; t3 = pid
	sll			$t3,$t3,#16				; shift into usable position
	ldi			$t1,#0						; t1 = count of consecutive empty buckets
	mov			$t2,$t3						; t2 = map entry number
	or			$t2,$t2,#OSPAGES	; start looking at page 32 (others are for OS)
	ldi			$t5,#511					; max number of pages - 1
	or			$t5,$t5,$t3				; t5 = max in ASID
.0001:
	mvmap		$v0,$x0,$t2				; get map entry into v0
	beq			$v0,$x0,.empty0		; is it empty?
	add			$t2,$t2,#1
	bltu		$t2,$t5,.0001
.0002:
	ldi			$v0,#-1						; got here so no run was found
	ret
.empty0:
	mov			$t3,$t2						; save first empty bucket
.empty1:
	bgeu		$t2,$t5,.0002
	add			$t1,$t1,#1
	bgeu		$t1,$a1,.foundEnough
	add			$t2,$t2,#1				; next bucket
	mvmap		$v0,$x0,$t2				; get map entry
	beq			$v0,$x0,.empty1
	mov			$t1,$x0						; reset counter
	bra			.0001							; go back and find another run
.foundEnough:
	mov			$v0,$t3						; v0 = start of run
	ret

;------------------------------------------------------------------------------
; Parameters:
;		a0 = pid
;		a1 = amount of memory to allocate
; Modifies:
;		t0
; Returns:
;		v1 = pointer to allocated memory in virtual address space.
;		v0 = E_Ok for success, E_NotAlloc otherwise
;------------------------------------------------------------------------------
;
Alloc:
	sub			$sp,$sp,#16
	sw			$ra,[$sp]
	sw			$s1,4[$sp]				; these regs must be saved
	sw			$s2,8[$sp]
	sw			$s3,12[$sp]
	; First check if there are enough pages available in the system.
	add			$v0,$a1,#PAGESZ-1	; v0 = round memory request
	srl			$v0,$v0,#LOG_PGSZ	; v0 = convert to pages required
	lw			$t0,NPAGES				; check number of pages available
	bleu		$v0,$t0,.enough
.noRun2:
	ldi			$v1,#0						; not enough, return null
	bra			.noRun
.enough:
	mov			$s1,$a0
	mov			$a0,$s1
	; There are enough pages, but is there a run long enough in map space?
	mov			$s2,$v0				; save required # pages
	mov			$a1,$v0
	call		FindRun						; find a run of available slots
	blt			$v0,$x0,.noRun2
	; Now there are enough pages, and a run available, so allocate
	mov			$s1,$v0						; s1 = start of run
	lw			$s3,NPAGES				; decrease number of pages available in system
	sub			$s3,$s3,$s2
	sw			$s3,NPAGES
	mov			$s3,$v0						; s3 = start of run
.0001:
	palloc	$v0								; allocate a page (cheat and use hardware)
	;call		AllocPage
	beq			$v0,$x0,.noRun
	mvmap		$x0,$v0,$s3				; map the page
	add			$s3,$s3,#1				; next bucket
	sub			$s2,$s2,#1
	bne			$s2,$x0,.0001
	sll			$v1,$s1,#LOG_PGSZ	; v0 = virtual address of allocated mem.
	ldi			$v0,#E_Ok
	bra			.xit
.noRun:
	ldi			$v0,#E_NotAlloc
.xit
	lw			$ra,[$sp]					; restore saved regs
	lw			s1,4[$sp]
	lw			s2,8[$sp]
	lw			s3,12[$sp]
	add			$sp,$sp,#16
	ret

;------------------------------------------------------------------------------
; Allocate the stack page for a task. The stack is located at the highest
; virtual address ($7FC00).
;
; Parameters:
;		a0 = pid to allocate for
;	Returns:
;		v0 = physical address, 0 if unsuccessful
;		v1 = virtual address, not valid unless successful
;------------------------------------------------------------------------------

AllocStack:
	sll			$v1,$a0,#16			; 
	or			$v1,$v1,#511		; last page of memory is for stack
	mvmap		$v0,$x0,$v1			; check if stack already allocated
	bnez		$v0,.0001
	palloc	$v0							; allocate a page
	beqz		$v0,.xit		    ; success?
	mvmap		$x0,$v0,$v1
.0001:
	and			$v1,$v1,#511
	sll			$v0,$v0,#LOG_PGSZ			; convert pages to addresses
	sll			$v1,$v1,#LOG_PGSZ
.xit:
	ret

;------------------------------------------------------------------------------
; This routine will de-allocate all the pages associated with a task including
; the stack.
;
; Parameters:
;		a0 = pid to free memory for
;	Modifies:
;		a0,t0,t1,t3,t4
; Returns:
;		none
;------------------------------------------------------------------------------

FreeAll:
	ldi			$t3,#0
	sll			$t4,$a0,#16
.nxt:
	slt			$t1,$t3,#512		; number of buckets to check
	beqz		$t1,.0001
	and			$t4,$t4,#$F0000
	or			$t4,$t4,$t3			; combine pid and bucket number
	ldi			$t0,#0					; new page number to set (indicates free)
	mvmap		$t0,$t0,$t4			; get page mapping and set to zero
	add			$t3,$t3,#1			; advance to next bucket
	beqz		$t0,.nxt				; 0 = no map in this bucket
	pfree		$t0							; free the page
	lw			$t0,NPAGES			; update the number of available pages
	add			$t0,$t0,#1
	sw			$t0,NPAGES
	bra			.nxt
.0001:
	ret

;------------------------------------------------------------------------------
; Convert a virtual address to a physical one
;
; Parameters:
;		a0 = virtual address to convert
; Modifies:
;		t0
; Returns:
;		v0 = physcial address
;------------------------------------------------------------------------------

VirtToPhys:
	blt		$a0,$x0,.notMapped
	srl		$t0,$a0,#LOG_PGSZ		; convert virt to page
	mGetCurrentTid
	sll		$v0,$v0,#16
	or		$v0,$v0,$t0					; and in tid
	mvmap	$v0,$x0,$v0					; get the translation
	sll		$v0,$v0,#LOG_PGSZ		; convert page to address
	and		$t0,$a0,#$3FF				; insert LSB's
	or		$v0,$v0,$t0
	ret
.notMapped:
	mov		$v0,$a0
	ret
