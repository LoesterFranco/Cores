; ============================================================================
;        __
;   \\__/ o\    (C) 2020  Robert Finch, Stratford
;    \  __ /    All rights reserved.
;     \/_//     robfinch<remove>@finitron.ca
;       ||
;  
;
; This source file is free software: you can redistribute it and/or modify 
; it under the terms of the GNU Lesser General Public License as published 
; by the Free Software Foundation, either version 3 of the License, or     
; (at your option) any later version.                                      
;                                                                          
; This source file is distributed in the hope that it will be useful,      
; but WITHOUT ANY WARRANTY; without even the implied warranty of           
; MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the            
; GNU General Public License for more details.                             
;                                                                          
; You should have received a copy of the GNU General Public License        
; along with this program.  If not, see <http://www.gnu.org/licenses/>.    
;
; ============================================================================

	code
	align	4
OSCallTbl:
	dw		FMTK_Initialize					; 0
	dw		FMTK_StartTask					; 1
	dw		FMTK_ExitTask
	dw		FMTK_KillTask
	dw		FMTK_SetTaskPriority
	dw		FMTK_Sleep							; 5
	dw		FMTK_AllocMbx
	dw		FMTK_FreeMbx
	dw		FMTK_PostMsg
	dw		FMTK_SendMsg
	dw		FMTK_WaitMsg						; 10
	dw		FMTK_PeekMsg
	dw		FMTK_StartApp           ; 12
	dw		FMTK_SchedulerIRQ 			; 13
	dw		FMTK_GetCurrentTid
	dw		DumpReadyQueue
	dw		0
	dw		0
	dw		0
	dw		0
	dw		FMTK_HasIOFocus					; 20
	dw		FMTK_SwitchIOFocus			; 21
	dw		FMTK_ReleaseIOFocus			; 22
	dw		FMTK_ForceReleaseIOFocus	; 23
	dw		FMTK_RequestIOFocus			; 24
	dw		0
	dw		FMTK_IO									; 26
	dw    FMTK_GetDCBField        ; 27
	dw    FMTK_SetDCBField        ; 28
	dw    0
	dw    0
	dw    0
	dw    GetPamBit               ; 32
	dw    FetchMemoryWord         ; 33
	dw    Monitor                 ; 34
	dw    GetIRQFlag              ; 35
	dw    SetIRQFlag              ; 36

qToChk:
	db	0,0,0,2,0,0,4,2
	db	0,0,6,2,0,0,4,2
	db	0,0,0,2,0,0,4,2
	db	0,0,6,2,0,0,4,2

	align	4

;------------------------------------------------------------------------------
;------------------------------------------------------------------------------

FMTKInit:
	stt		$x0,QNDX
	stt		$x0,PIDMAP
	stt   $x0,APPID_POOL
	stt		$x0,missed_ticks
	ldi		$t0,#0
	stt   $x0,CurrentTid
	stt		$t0,TimeoutList
	stt		$t0,READYQ
	stt		$t0,READYQ+4
	stt		$t0,READYQ+8
	stt		$t0,READYQ+12
	
	stt		$x0,IOFocusTbl
	stt		$t0,IOFocusNdx
	
	; zero out device function table
	ldi		$t0,#DVF_Base
	ldi		$t1,#32*32
.0003:
	stt		$x0,[$t0]
	add		$t0,$t0,#4
	sub		$t1,$t1,#1
	bgtz	$t1,.0003

	; Initialize mailboxes
	ldi		$t0,#mbxs
	ldi		$t1,#4*32
.0001:
	stt		$x0,[$t0]
	add		$t0,$t0,#4
	sub		$t1,$t1,#1
	bgtz	$t1,.0001

	; Initialize free message list
	ldi		$t0,#msgs
	stt		$t0,FreeMsg
	ldi		$t1,#0
	ldi		$t2,#512
.0002:
	add		$t1,$t1,#1
	stt		$t1,MSG_LINK[$t0]
	add		$t0,$t0,#16
	sub		$t2,$t2,#1
	bgtz	$t2,.0002

	; unlock the system semaphore	
	mUnlockSemaphore(SysSema)
	ret

FMTK_Initialize:
  call  FMTKInit
  jmp   ERETx
 
;------------------------------------------------------------------------------
; Get the task id for the currently running task.
;
; Returns:
;		v0 = task id
;------------------------------------------------------------------------------

GetCurrentTid:
;	csrrw	$v0,#$181,$x0				; get current pid
;	and		$v0,$v0,#15					; mask off extra bits
	ldt   $v0,CurrentTid
	ret

FMTK_GetCurrentTid:
	mGetCurrentTid
	mov		$v1,$v0
	ldi		$v0,#E_Ok
	jmp		ERETx

GetIRQFlag:
  ldt   $v1,IRQFlag
  ldi   $v0,#E_Ok
  jmp   ERETx

SetIRQFlag:
  stt   $a1,IRQFlag
  ldi   $v0,#E_Ok
  jmp   ERETx

;------------------------------------------------------------------------------
; Parameters:
;		a0 = task id
;------------------------------------------------------------------------------

MapOSPages:
	ldi			$v0,#OSPAGES	; number of pages pre-mapped
	ldi			$v1,#0
	sll			$v1,$a0,#16		; put ASID in proper spot
.nxt:
	mvmap		$x0,$v1,$v1
	add			$v1,$v1,#1
	sub			$v0,$v0,#1
	bgtz		$v0,.nxt
	ret

;------------------------------------------------------------------------------
; Select the next task to run. The ready lists are searched in a circular
; fashion beginning with the list identified indirectly by QNDX. There are
; four ready lists to hold tasks of four different priorities. 
;
; Parameters:
;		none
; Modifies:
;		v1, t0, t1, t2, t3, t4
;	Returns:
;		v0 = task id of task to run
;------------------------------------------------------------------------------

SelectTaskToRun:
	; Pick the first queue to check, occasionally the queue
	; chosen isn't the highest priority one in order to 
	; prevent starvation of lower priority tasks.
	ldbu	$v1,QNDX						; get index into que check table
	add		$v1,$v1,#1					; increment it, and limit
	and		$v1,$v1,#31
	stb		$v1,QNDX						; store back
	ldbu	$v1,qToChk[$v1]			; assume this will be valid
	ldi		$t2,#4							; 4 queues to check
.nxtQ:
  popq  $v0,$v1
  bnez  $v0,.dq
;	getrdy	$v0,$x0,$v1
;	ldt		$v0,READYQ[$v1]			; check queue to see if contains any
;	bnez	$v0,.dq					    ; yes, go dequeue
;.0001:
	add		$v1,$v1,#1					; no, advance to next queue
	and		$v1,$v1,#3					; 4 max
	sub		$t2,$t2,#1					;
	bgtz	$t2,.nxtQ				    ; go back to check next queue
	; Here, nothing else is actually ready to run
	; just go back to what we were doing.
	mGetCurrentTid
	ret
.dq:
  ; TCB's are allocated in 1kB pages and are evenly aligned. If there
  ; are some address bits set that shouldn't be just skip over the
  ; selection. There is only 512k ram in the system. Higher order
  ; address bits should also be zero.
  and   $t0,$v0,#$FFF003FF
  bnez  $t0,.nxtQ

  ; If we got a task but it isn't actually ready anymore go back
  ; and get another task
  ldb   $t0,TCBStatus[$v0]  ;
  and   $t0,$t0,#TS_READY
  beqz  $t0,.nxtQ
  ; The task was ready and we removed it from the queue by popping
  ; it so push it back on the queue.
  pushq $v0,$v1

;	ldi		$t3,#MAX_TID				; ensure we have a valid tid
;	bleu	$v0,$t3,.goodTid
	; If the tid isn't valid the readyq was screwed up
;	ldi		$t3,#-1							; indicate queue empty
;	stw		$t3,READYQ[$v1]
;	bra		.0001								; and try next queue
.goodTid:
	;sll		$t1,$v0,#LOG_TCBSZ
	ldt		$t0,TCBNext[$v0]		; update head of ready queue
	stt		$t0,READYQ[$v1]
	ret

;------------------------------------------------------------------------------
; Swap from outgoing context to incoming context.
;
; Parameters:
;		a0 = pointer to TCB of outgoing context
;		a1 = pointer to TCB of incoming context
;------------------------------------------------------------------------------

SwapContext:
	; Save outgoing register set in TCB
	csrrs	$x0,#$780,#4	; select user register set for Rs2
	stt		$x1,TCBIntRegs+4[$a0]
	stt		$x2,TCBIntRegs+8[$a0]
	stt		$x3,TCBIntRegs+12[$a0]
	stt		$x4,TCBIntRegs+16[$a0]
	stt		$x5,TCBIntRegs+20[$a0]
	stt		$x6,TCBIntRegs+24[$a0]
	stt		$x7,TCBIntRegs+28[$a0]
	stt		$x8,TCBIntRegs+32[$a0]
	stt		$x9,TCBIntRegs+36[$a0]
	stt		$x10,TCBIntRegs+40[$a0]
	stt		$x11,TCBIntRegs+44[$a0]
	stt		$x12,TCBIntRegs+48[$a0]
	stt		$x13,TCBIntRegs+52[$a0]
	stt		$x14,TCBIntRegs+56[$a0]
	stt		$x15,TCBIntRegs+60[$a0]
	stt		$x16,TCBIntRegs+64[$a0]
	stt		$x17,TCBIntRegs+68[$a0]
	stt		$x18,TCBIntRegs+72[$a0]
	stt		$x19,TCBIntRegs+76[$a0]
	stt		$x20,TCBIntRegs+80[$a0]
	stt		$x21,TCBIntRegs+84[$a0]
	stt		$x22,TCBIntRegs+88[$a0]
	stt		$x23,TCBIntRegs+92[$a0]
	stt		$x24,TCBIntRegs+96[$a0]
	stt		$x25,TCBIntRegs+100[$a0]
	stt		$x26,TCBIntRegs+104[$a0]
	stt		$x27,TCBIntRegs+108[$a0]
	stt		$x28,TCBIntRegs+112[$a0]
	stt		$x29,TCBIntRegs+116[$a0]
	stt		$x30,TCBIntRegs+120[$a0]
	stt		$x31,TCBIntRegs+124[$a0]
	csrrc	$x0,#$780,#4				; select machine register set for Rs2
	csrrw	$x2,#$341,$x0				; save off mepc
	stt		$x2,TCBepc[$a0]
	; Now save off segment registers
	ldi		$t1,#0
.svseg:
	mvseg	$t0,$x0,$t1
	sll		$x2,$t1,#2
	add		$x2,$x2,$a0
	stt		$t0,TCBsegs[$x2]
	add		$t1,$t1,#1
	and		$t1,$t1,#15
	bnez	$t1,.svseg

	; Switch memory maps
;	srl		$v0,$a1,#10					; convert pointer to tid
;	and		$v0,$v0,#$F					; mask to 16 task
  ldb   $v0,TCBMap[$a1]
	csrrw	$x0,#$181,$v0				; set ASID
	stt   $a1,CurrentTid
	; User map has now been switched

	; Restore segment register set
	ldi		$t1,#0
.rsseg:
	sll		$x2,$t1,#2
	add		$x2,$x2,$a1
	ldt		$t0,TCBsegs[$x2]
	mvseg	$x0,$t0,$t1
	add		$t1,$t1,#1
	and		$t1,$t1,#15
	bnez	$t1,.rsseg

	ldt		$x2,TCBepc[$a1]			; restore epc
	csrrw	$x0,#$341,$x2
	; Restore incoming registers
	csrrs	$x0,#$780,#1				; select user regset for Rd
	ldt		$x1,TCBIntRegs+4[$a1]
	ldt		$x2,TCBIntRegs+8[$a1]
	ldt		$x3,TCBIntRegs+12[$a1]
	ldt		$x4,TCBIntRegs+16[$a1]
	ldt		$x5,TCBIntRegs+20[$a1]
	ldt		$x6,TCBIntRegs+24[$a1]
	ldt		$x7,TCBIntRegs+28[$a1]
	ldt		$x8,TCBIntRegs+32[$a1]
	ldt		$x9,TCBIntRegs+36[$a1]
	ldt		$x10,TCBIntRegs+40[$a1]
	ldt		$x11,TCBIntRegs+44[$a1]
	ldt		$x12,TCBIntRegs+48[$a1]
	ldt		$x13,TCBIntRegs+52[$a1]
	ldt		$x14,TCBIntRegs+56[$a1]
	ldt		$x15,TCBIntRegs+60[$a1]
	ldt		$x16,TCBIntRegs+64[$a1]
	ldt		$x17,TCBIntRegs+68[$a1]
	ldt		$x18,TCBIntRegs+72[$a1]
	ldt		$x19,TCBIntRegs+76[$a1]
	ldt		$x20,TCBIntRegs+80[$a1]
	ldt		$x21,TCBIntRegs+84[$a1]
	ldt		$x22,TCBIntRegs+88[$a1]
	ldt		$x23,TCBIntRegs+92[$a1]
	ldt		$x24,TCBIntRegs+96[$a1]
	ldt		$x25,TCBIntRegs+100[$a1]
	ldt		$x26,TCBIntRegs+104[$a1]
	ldt		$x27,TCBIntRegs+108[$a1]
	ldt		$x28,TCBIntRegs+112[$a1]
	ldt		$x29,TCBIntRegs+116[$a1]
	ldt		$x30,TCBIntRegs+120[$a1]
	ldt		$x31,TCBIntRegs+124[$a1]
	csrrc	$x0,#$780,#1				; select machine regset for Rd
	ret

;------------------------------------------------------------------------------
; Operating system call dispatcher.
; On entry machine registers are selected.
;------------------------------------------------------------------------------

OSCALL:
	csrrs	$t1,#$780,#2		; select user regs for Rs1		
	mov		$a0,$a0					; move user to machine
	stt   $t1,$4380
	mov		$a1,$a1
	mov		$a2,$a2
	mov		$a3,$a3
	mov		$a4,$a4
	mov		$a5,$a5
	csrrc	$x0,#$780,#15		; get back machine registers for all
	and		$a0,$a0,#63
	sll		$a0,$a0,#2
	ldt	  $t0,OSCallTbl[$a0]
	beqz  $t0,ERETx
	jmp		[$t0]

;------------------------------------------------------------------------------
; Time accounting.
; Update the length of time the task has been running.
;
; Parameters:
;		s1 = pointer to TCB
; Modifies:
;		t2,t3,t4,t5
;------------------------------------------------------------------------------

AccountTime:
.again:
;	csrrw	$t3,#$741,$x0					; get high time
;	csrrw	$t2,#$701,$x0					; get low time
;	csrrw	$t4,#$741,$x0
;	bne		$t3,$t4,.again
	ldt		$t2,Tick
	stt		$t2,TCBEndTick[$s1]
	ldt		$t3,TCBStartTick[$s1]
	sub		$t4,$t2,$t3						; end - start
	ldt		$t5,TCBTicks[$s1]
	add		$t5,$t5,$t4						; ticks + (end - start)
	stt		$t5,TCBTicks[$s1]
	ret

;------------------------------------------------------------------------------
; Sleep for a number of ticks. Tick interval determined by the VIA timer #3.
; Passing a time of zero or less causes the function to return right away.
;
; Parameters:
;		a1 = length of time to sleep (must be >= 0)
; Returns:
;		none
;------------------------------------------------------------------------------

FMTK_Sleep:
	bltz	$a1,ERETx
	mGetCurrentTid
;	sll		$s1,$v0,#LOG_TCBSZ
	mov   $s1,$v0
	beqz	$a1,.0001
	mov		$a0,$v0								; a0 = current tid
	call	RemoveFromReadyQueue
	call	InsertIntoTimeoutList	; a1 = timeout
.0001:
	ldbu	$v0,TCBStatus[$s1]		; flag task as no longer running
	and		$v0,$v0,#~TS_RUNNING
	stb		$v0,TCBStatus[$s1]

	call	AccountTime						; uses s1
	call	SelectTaskToRun

;	sll		$s2,$v0,#LOG_TCBSZ		; s2 = pointer to incoming TCB
	mov   $s2,$v0
	ldbu	$t2,TCBStatus[$s2]		; x2 = incoming status
	or		$t2,$t2,#TS_RUNNING|TS_READY	; set status = running
	stb   $t2,TCBStatus[$s2]
	ldt		$x2,TCBException[$s2]	;
	beqz	$x2,.noException
	; set link register to catch handler address
	;{
	;	t->regs[29] = t->regs[28];   // set link register to catch handler
	;	t->epc = t->regs[28];        // and the PC register
	;	t->regs[1] = t->exception;    // r1 = exception value
	;	t->exception = 0;
	;	t->regs[2] = 45;              // r2 = exception type
	;}
	stt		$x2,4[$s2]						; r1 = exception
	stt		$x0,TCBException[$s2]	; tcb->exception = 0
	ldi		$x2,#45
	stt		$x2,8[$s2]						; r2 = 45
.noException:

	; If a message is ready, update status to ready and put
	; message in target memory. The task will be returning
	; from a WaitMsg so a return status of E_Ok is also set.
	and		$x2,$t2,#TS_MSGRDY
	beqz	$x2,.noMsg
	ldt		$a0,80[$s2]					; user a2 (x20)
	beqz	$a0,.0002
	call	VirtToPhys
	ldt		$x2,TCBMsgD1[$s2]
	stt		$x2,[$v0]
.0002:
	ldt		$a0,84[$s2]
	beqz	$a0,.0003
	call	VirtToPhys
	ldt		$x2,TCBMsgD2[$s2]
	stt		$x2,[$v0]
.0003:
	ldt		$a0,88[$s2]
	beqz	$a0,.0004
	call	VirtToPhys
	ldt		$x2,TCBMsgD3[$s2]
	stt		$x2,[$v0]
.0004:
	ldi		$x2,#E_Ok						; setup to return E_Ok
	stt		$x2,64[$s2]					; in v0

.noMsg:
	and		$t2,$t2,#~TS_MSGRDY		; mask out message ready status
	stb		$t2,TCBStatus[$s2]
	beq		$s1,$s2,.noCtxSwitch	; incoming and outgoing contexts the same?
	mov		$a0,$s1
	mov		$a1,$s2
	call	SwapContext
.noCtxSwitch:
	ldt		$t2,Tick						; get tick
	stt		$t2,TCBStartTick[$s1]
	ldi   $v0,#E_Ok
	jmp		ERETx

;------------------------------------------------------------------------------
; SchedulerIRQ meant to be called from the timer ISR. This routine will not
; be invoked if the operating system is active.
; Does not return anything.
; The user mode task may be switched to the highest priority task.
;------------------------------------------------------------------------------

FMTK_SchedulerIRQ:
	decto
	sub		$sp,$sp,#4
	stt		$ra,[$sp]
	ldi		$a0,#SysSema
	ldi		$a1,#20
	mGetCurrentTid
	mov   $s1,$v0
;	sll		$s1,$v0,#LOG_TCBSZ		; compute pointer to TCB
;	call	LockSemaphore
;	beq		$v0,$x0,.noLock
; Might need the following if the external timer isn't used.
;	csrrw	$v0,#$701,$x0					; get the time
;	add		$v0,$v0,#600000				; wait 600,000 cycles @20MHz (30ms)
;	csrrw	$x0,#$321,$v0					; set next interrupt time
	ldt		$t5,Tick							; update tick count
	add		$t5,$t5,#1
	stt		$t5,Tick
	call	AccountTime
	ldbu	$t5,TCBStatus[$s1]
	or		$t5,$t5,#TS_PREEMPT
	and		$t5,$t5,#~TS_RUNNING	; no longer running, but may still be ready
	stb		$t5,TCBStatus[$s1]
	bra   .bypass
	; Keep popping the timeout list as long as there are tasks on it with
	; expired timeouts.
	ldi		$s5,#0
	ldi		$s3,#1
	getzl	$s4
.0001:
;	sll		$t4,$s5,#10
	sll   $s5,$s5,#2          ; $s5 = index
	ldt   $t4,TidTcbMap[$s5]
	srl   $s5,$s5,#2          ; $s5 = mid
	lbu		$t2,TCBStatus[$t4]
	and		$t2,$t2,#TS_TIMEOUT
	beqz	$t2,.noTimeout
	and		$t2,$s4,$s3					; is zero bit set?
	beqz	$t2,.notZero
	ldbu	$t2,TCBStatus[$t4]
	and		$t2,$t2,#~(TS_TIMEOUT|TS_WAITMSG)
	stb		$t2,TCBStatus[$t4]
	mov		$a0,$t4
	mMbxRemoveTask
	call	InsertIntoReadyQueue
.notZero:
.noTimeout:
	add		$s5,$s5,#1
	sll		$s3,$s3,#1	
	slt		$t2,$s5,#NR_TCB
	bnez	$t2,.0001
.bypass:
	ldt		$v0,TimeoutList
	beqz  $v0,.noTimeouts
;	ldi		$t4,#NR_TCB
;	bgeu	$v0,$t4,.noTimeouts
;	sll		$t4,$v0,#LOG_TCBSZ	; index to pointer
	ldt		$t3,TCBTimeout[$v0]
	bgtz	$t3,.timeoutNotDone
	mPopTimeoutList						; popping timeout list modifies head of it
	mov		$a0,$v0
	call	InsertIntoReadyQueue
	bra		.bypass
.timeoutNotDone:
	sub		$t3,$t3,#1
	ldt		$t2,missed_ticks
	sub		$t3,$t3,$t2
	stt		$t3,TCBTimeout[$v0]
	stt		$x0,missed_ticks
.noTimeouts:
	; The ready queue was just updated, there could be new tasks
	; ready to run.
	call	SelectTaskToRun

;	sll		$s2,$v0,#LOG_TCBSZ	; s2 = pointer to incoming TCB
  mov   $s2,$v0
	lbu		$x2,TCBStatus[$s2]	; x2 = incoming status
	or		$t2,$x2,#TS_RUNNING|TS_READY	; status = running
	ldt		$x2,TCBException[$s2]	;
	beqz	$x2,.noException
	; set link register to catch handler address
	;{
	;	t->regs[29] = t->regs[28];   // set link register to catch handler
	;	t->epc = t->regs[28];        // and the PC register
	;	t->regs[1] = t->exception;    // r1 = exception value
	;	t->exception = 0;
	;	t->regs[2] = 45;              // r2 = exception type
	;}
.noException:

	; If a message is ready, update status to ready and put
	; message in target memory. The task will be returning
	; from a WaitMsg so a return status of E_Ok is also set.
	bra		.noMsg
	and		$x2,$t2,#TS_MSGRDY
	beq		$x2,$x0,.noMsg
	ldt		$a0,80[$s2]					; user a2 (x20)
	beq		$a0,$x0,.0002
	call	VirtToPhys
	ldt		$x2,TCBMsgD1[$s2]
	stt		$x2,[$v0]
.0002:
	ldt		$a0,84[$s2]
	beq		$a0,$x0,.0003
	call	VirtToPhys
	ldt		$x2,TCBMsgD2[$s2]
	stt		$x2,[$v0]
.0003:
	ldt		$a0,88[$s2]
	beq		$a0,$x0,.0004
	call	VirtToPhys
	ldt		$x2,TCBMsgD3[$s2]
	stt		$x2,[$v0]
.0004:
	ldi		$x2,#E_Ok						; setup to return E_Ok
	stt		$x2,64[$s2]					; in v0

.noMsg:
	and		$t2,$t2,#~TS_MSGRDY		; mask out message ready status
	stb		$t2,TCBStatus[$s2]
	beq		$s1,$s2,.noCtxSwitch
	mov		$a0,$s1
	mov		$a1,$s2
	call	SwapContext
.noCtxSwitch:
;	mUnlockSemaphore(SysSema)
.noLock:
	ldt		$ra,[$sp]
	add		$sp,$sp,#4
	ldt		$t2,Tick					; get tick
	stt		$t2,TCBStartTick[$s1] ;[$s1]
	ret

;------------------------------------------------------------------------------
; Start a task.
;	Task status is set to ready, priority normal, and the task is inserted into
; the ready list. Segment registers are setup for a flat memory model.
; 
;	Parameters:
;		a1 = memory required (task's local memory)
;		a2 = start pc (usually $400)
;	Modifies:
;		a0 = tid
;	Returns:
;		v0 = E_Ok if successful
;		v1 = tid of started task if successful
;------------------------------------------------------------------------------
;
FMTK_StartTask:
  mov   $s2,$a1
  ldi   $a0,#0        ; mid = 0 = OS memory map  
  ldi   $a1,#1024
  call  Alloc         ; allocate storage for the TCB
;  call  AllocTCB
	bnez	$v0,.err
	mov		$a0,$v1
  call  AllocTCB
  stb   $v1,TCBMap[$a0]
  sll   $v1,$v1,#2          ; convert Tid to map index
  stt   $a0,TidTcbMap[$v1]  ; store the TCB pointer in the map
  mov   $s1,$a0             ; $s1 = pointer to TCB
  srl   $v1,$v1,#2          ; restore Tid
;	call	FreeAll
;	call	MapOSPages			; Map OS pages into address space
  mov   $a0,$v1         ; $a0 = mid
  mov   $a1,$s2
;	sll		$s1,$a0,#LOG_TCBSZ	; compute TCB address
	call	AllocStack
	or		$t0,$v0,#$A0000FFC	; set stack pointer
	stt		$t0,TCBIntRegs+56[$s1]
	stt		$a2,TCBepc[$s1]	; address task will begin at
	call	Alloc
	stb		$v0,$4320
	bnez	$v0,.err
	ldi		$t0,#TS_READY
	stb		$t0,TCBStatus[$s1]
	ldi		$t0,#2					; normal execution priority
	stb		$t0,TCBPriority[$s1]
	; leave segment base at $0, flat memory model
	ldi		$t0,#6							; read,write
	stt		$t0,TCBsegs[$s1]			; segs 0 to 11
	stt		$t0,TCBsegs+4[$s1]
	stt		$t0,TCBsegs+8[$s1]
	stt		$t0,TCBsegs+12[$s1]
	stt		$t0,TCBsegs+16[$s1]
	stt		$t0,TCBsegs+20[$s1]
	stt		$t0,TCBsegs+24[$s1]
	stt		$t0,TCBsegs+28[$s1]
	stt		$t0,TCBsegs+32[$s1]
	stt		$t0,TCBsegs+36[$s1]
	stt		$t0,TCBsegs+40[$s1]
	stt		$t0,TCBsegs+44[$s1]
	ldi		$t0,#5							; read,execute
	stt		$t0,TCBsegs+48[$s1]	; segs 12 to 15
	stt		$t0,TCBsegs+52[$s1]
	stt		$t0,TCBsegs+56[$s1]
	stt		$t0,TCBsegs+60[$s1]
;	srl		$a0,$s1,#LOG_TCBSZ	; need the tid again
  mov   $a0,$s1
	call	InsertIntoReadyQueue
	mov		$v1,$a0
	stt		$v1,$4320
.err:
ERETx:
	csrrs	$x0,#$780,#1				; select user for destination
	mov		$v1,$v1							; move return values to user registers
	mov		$v0,$v0
	jmp   ERETx2

;------------------------------------------------------------------------------
; Exit the current task.
;
; Parameters:
;		none
; Modifies:
;		a1 = task id
;------------------------------------------------------------------------------

FMTK_ExitTask:
	mGetCurrentTid
	mov		$a1,$v0
	; fall through to KillTask
	
;------------------------------------------------------------------------------
; Parameters:
;		a1 = tid of task to kill
;------------------------------------------------------------------------------

FMTK_KillTask:
;	beqz	$a1,.immortal		    ; tid #0 is immortal (the system)
	ldi		$t0,#TS_DEAD				; flag task as dead (prevents it from being re-queued)
;	and		$t1,$a1,#15					; limit pid
;	sll		$t1,$t1,#LOG_TCBSZ	; convert to TCB address
	stb		$t0,TCBStatus[$a1]
	mov		$a0,$a1							; $a0 = pid
	call	FreeAll							; free all the memory associated with the task
	; Now make process ID available for reuse
	ldb   $a0,TCBMap[$a1]
	call  FreeTCB
;	ldwu	$t1,PIDMAP
;	ldi		$t0,#1							; generate bit "off" mask
;	sll		$t0,$t0,$a1
;	xor		$t0,$t0,#-1					; complment for inverted mask
;	and		$t1,$t1,$t0
;	stw		$t1,PIDMAP
.immortal:
	ldi		$v0,#E_Ok
	jmp		ERETx

	