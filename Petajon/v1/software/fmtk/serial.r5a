; ============================================================================
;        __
;   \\__/ o\    (C) 2020  Robert Finch, Stratford
;    \  __ /    All rights reserved.
;     \/_//     robfinch<remove>@finitron.ca
;       ||
;  
;
; This source file is free software: you can redistribute it and/or modify 
; it under the terms of the GNU Lesser General Public License as published 
; by the Free Software Foundation, either version 3 of the License, or     
; (at your option) any later version.                                      
;                                                                          
; This source file is distributed in the hope that it will be useful,      
; but WITHOUT ANY WARRANTY; without even the implied warranty of           
; MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the            
; GNU General Public License for more details.                             
;                                                                          
; You should have received a copy of the GNU General Public License        
; along with this program.  If not, see <http://www.gnu.org/licenses/>.    
;
; ============================================================================

;------------------------------------------------------------------------------
;------------------------------------------------------------------------------
; Device command 
;
	code
	align	8
SerialFuncTbl:
	dco		0							; no operation
	dco		0							; setup
	dco		0							; initialize
	dco		0							; status
	dco		0							; media check
	dco		0							; build BPB
	dco		0							; open
	dco		0							; close
	dco		SerialGetChar	; get char
	dco		SerialPeekChar
	dco		0							; get char direct
	dco		SerialPeekCharDirect	; peek char direct
	dco		0							; input status
	dco		SerialPutChar
	dco		0							; reserved
	dco		0							; set position
	dco		0							; read block
	dco		0							; write block
	dco		0							; verify block
	dco		0							; output status
	dco		0							; flush input
	dco		0							; flush output
	dco		SerialIRQ			; IRQ routine
	dco		0							; Is removable
	dco		0							; ioctrl read
	dco		0							; ioctrl write
	dco		0							; output until busy
	dco		0							; 27
	dco		0
	dco		0
	dco		0
	dco		0							; 31

MAX_DEV_OP			EQU		31

;------------------------------------------------------------------------------
; Initialize serial port.
;
; Modifies:
;		$t0
;------------------------------------------------------------------------------

SerialInit:
	sub		$sp,$sp,#8
	sto		$ra,[$sp]
	ldi		$a0,#5							; serial device
	ldi		$a1,#SerialFuncTbl
	call	CopyDevFuncTbl
	stt		$x0,SerHeadRcv
	stt		$x0,SerTailRcv
	stt		$x0,SerHeadXmit
	stt		$x0,SerTailXmit
	stb		$x0,SerRcvXon
	stb		$x0,SerRcvXoff
	ldi		$t0,#$09						; dtr,rts active, rxint enabled, no parity
	stt		$t0,UART+8
	ldi		$t0,#$0006001E			; reset the fifo's
	stt		$t0,UART+12
	ldi		$t0,#$0000001E			; baud 9600, 1 stop bit, 8 bit, internal baud gen
	stt		$t0,UART+12
	ldo		$ra,[$sp]
	add		$sp,$sp,#8
	ret
		
;------------------------------------------------------------------------------
;------------------------------------------------------------------------------

SerialServiceInit:
	ldi		$a0,#1			; start task
	ldi		$a1,#1024		; memory required
	ldi		$a2,#SerialService
	ecall
	ret
SerialService:
	sub		$sp,$sp,#512+24
	ldi		$a0,#14			; get current tid
	ecall
	add		$a2,$sp,#516
	mov		$a1,$v0
	ldi		$a0,#6			; alloc mailbox
	ecall

SerialServiceLoop:
	ldi		$a0,#10			; waitmsg
	add		$a1,$sp,#516
	add		$a2,$sp,#520
	add		$a3,$sp,#524
	add		$a4,$sp,#528
	ldi		$a5,#-1
	ecall

	lw		$t0,[$sp]
	and		$t0,$t0,#31
	sll		$t0,$t0,#1
	lw		$t0,SerialFuncTbl[$t0]
	jmp		[$t0]

SerialFinishCmd:
	lw		$a1,12[$sp]		; reply mbx
	add		$a1,$a1,#1		; -1 = no reply requested
	beq		$a1,$a0,.0001
	sub		$a1,$a1,#1
	ldi		$a0,#9				; sendmsg
	ldi		$a2,#-1
	ldi		$a3,#-1
	ldi		$a4,#-1
	ecall
.0001:
	jmp		SerialServiceLoop

;------------------------------------------------------------------------------
; SerialGetChar
;
; Check the serial port buffer to see if there's a char available. If there's
; a char available then return it. If the buffer is almost empty then send an
; XON.
;
; Stack Space:
;		3 words
; Parameters:
;		none
; Modifies:
;		none
; Returns:
;		$v0 = character or -1
;------------------------------------------------------------------------------

SerialGetChar:
		sub		$sp,$sp,#24
		sto		$ra,[$sp]
		sto		$v1,8[$sp]
		sto		$s1,16[$sp]
		lb		$s1,SerRcvXon				; skip sending XON if already sent
		csrrc	$s1,#$300,#1				; disable interrupts
		bne		$s1,$x0,.0002
		call	SerialRcvCount			; check number of chars in receive buffer
		slt		$v0,$v0,#8					; less than 8?
		beq		$v0,$x0,.0002
		ldi		$v0,#XON						; if <8 send an XON
		sb		$x0,SerRcvXoff			; clear XOFF status
		sb		$v0,SerRcvXon				; flag so we don't send it multiple times
		sb		$v0,UART+UART_TRB
.0002:
		lbu		$v1,SerHeadRcv			; check if anything is in buffer
		lbu		$v0,SerTailRcv
		beq		$v0,$v1,.noChars		; no?
		lb		$v0,SerRcvBuf[$v1]	; get byte from buffer
		add		$v1,$v1,#1					; update head index
		sb		$v1,SerHeadRcv				
		bra		.xit
.noChars:
.0001:
		ldi		$v0,#-1
.xit
		csrrw	$x0,#$300,$s1				; restore interrupts
		ldo		$ra,[$sp]
		ldo		$v1,8[$sp]
		ldo		$s1,16[$sp]
		add		$sp,$sp,#24
		ret

;------------------------------------------------------------------------------
; SerialPeekChar
;
; Check the serial port buffer to see if there's a char available. If there's
; a char available then return it. But don't update the buffer indexes. No need
; to send an XON here.
;
; Stack Space:
;		3 words
; Parameters:
;		none
; Modifies:
;		none
; Returns:
;		$v0 = character or -1
;------------------------------------------------------------------------------

SerialPeekChar:
	sub		$sp,$sp,#24
	sto		$ra,[$sp]
	sto		$v1,8[$sp]
	sto		$s1,16[$sp]
	csrrc	$s1,#$300,#1				; disable interrupts
	ldbu	$v1,SerHeadRcv			; check if anything is in buffer
	ldbu	$v0,SerTailRcv
	beq		$v0,$v1,.noChars		; no?
	ldb		$v0,SerRcvBuf[$v1]	; get byte from buffer
	bra		.xit
.noChars:
.0001:
	ldi		$v0,#-1
.xit
	csrrw	$x0,#$300,$s1				; restore interrupts
	ldo		$ra,[$sp]
	ldo		$v1,8[$sp]
	ldo		$s1,16[$sp]
	add		$sp,$sp,#24
	ret

;------------------------------------------------------------------------------
; SerialPeekChar
;		Get a character directly from the I/O port. This bypasses the input
; buffer.
;
; Stack Space:
;		3 words
; Parameters:
;		none
; Modifies:
;		none
; Returns:
;		$v0 = character or -1
;------------------------------------------------------------------------------

SerialPeekCharDirect:
	sub		$sp,$sp,#24
	sto		$ra,[$sp]
	sto		$v1,8[$sp]
	sto		$s1,16[$sp]
	csrrc	$s1,#$300,#1				; disable interrupts
	ldb		$v0,UART+UART_STAT
	and		$v0,$v0,#8					; look for Rx not empty
	beq		$v0,$x0,.0001
	ldb		$v0,UART+UART_TRB
	bra		.xit
.0001:
	ldi		$v0,#-1
.xit
	csrrw	$x0,#$300,$s1				; restore interrupts
	ldo		$ra,[$sp]
	ldo		$v1,8[$sp]
	ldo		$s1,16[$sp]
	add		$sp,$sp,#24
	ret

;------------------------------------------------------------------------------
; SerialPutChar
;    Put a character to the serial transmitter. This routine blocks until the
; transmitter is empty. The routine will attempt to transmit the char up to 
; 10 times. If it still can't transmit the char then sleep is called and the
; task is put to sleep for a tick. When it wakes up the routine continues to
; try and send a character.
;
; Stack Space
;		5 words
; Parameters:
;		$a3 = character to put
; Modifies:
;		none
;------------------------------------------------------------------------------

SerialPutChar:
	sub		$sp,$sp,#24
	sto		$v0,[$sp]
	sto		$ra,8[$sp]
	sto		$v1,16[$sp]
.0002:
	ldi		$v1,#10
.0001:
;	sub		$v1,$v1,#1
;	beq		$v1,$x0,.goSleep
	ldb		$v0,UART+UART_STAT	; wait until the uart indicates tx empty
	and		$v0,$v0,#16					; bit #4 of the status reg
	beqz	$v0,.0001						; branch if transmitter is not empty
	stb		$a3,UART+UART_TRB		; send the byte
	ldo		$v0,[$sp]
	ldo		$ra,8[$sp]
	ldo		$v1,16[$sp]
	add		$sp,$sp,#24
	ret
.goSleep:
	sub		$sp,$sp,#16
	sto		$a0,[$sp]
	sto		$a1,8[$sp]
	ldi		$a0,#5							; sleep function
	ldi		$a1,#1							; 1 tick
	ecall
	ldo		$a0,[$sp]
	ldo		$a1,8[$sp]
	add		$sp,$sp,#16
	bra		.0002
	
;------------------------------------------------------------------------------
; Calculate number of character in input buffer
;------------------------------------------------------------------------------

SerialRcvCount:
	ldbu	$v0,SerTailRcv	; v0 = tail index
	ldbu	$v1,SerHeadRcv	; v1 = head index
	sub		$t0,$v0,$v1
	bge		$t0,$x0,.xit
	ldi		$t0,#256
	sub		$t0,$t0,$v1
	add		$t0,$t0,$v0
.xit:
	mov		$v0,$t0
	ret
	
;------------------------------------------------------------------------------
;------------------------------------------------------------------------------

SerialIRQ:
.nxtByte:
	and		$t0,$a0,#$08				; bit 3 = rx full
	beq		$t0,$x0,.notRxInt
	lw		$a1,UART+UART_TRB		; get data from Rx buffer to clear interrupt
	lbu		$t2,SerHeadRcv			; get buffer indexes
	lbu		$t3,SerTailRcv
	add		$t3,$t3,#1					; see if buffer full
	and		$t3,$t3,#255
	beq		$t2,$t3,.rxFull
	sb		$t3,SerTailRcv			; update tail pointer
	sub		$t3,$t3,#1
	and		$t2,$t3,#255
	sb		$a1,SerRcvBuf[$t2]	; store recieved byte in buffer
	lb		$v0,SerRcvXoff			; check if xoff already sent
	bne		$v0,$x0,.0001
	call	SerialRcvCount			; if more than 240 chars in buffer
	slt		$v0,$v0,#240				; send an XOFF
	bne		$v0,$x0,.0001
	ldi		$a0,#XOFF
	sb		$x0,SerRcvXon				; clear XON status
	sb		$a0,SerRcvXoff			; set XOFF status
	sb		UART+UART_TRB
.0001:
	lw		$a0,UART+UART_STAT	; check the status for another byte
	bra		.nxtByte
;	ldi		$a0,#$0B						; dtr,rts active, rxint disabled, no parity
;	sw		$a0,UART+UART_CMD
.rxFull:
.notRxInt:
	eret
	
nmeSerial:
	db		"Serial",0
